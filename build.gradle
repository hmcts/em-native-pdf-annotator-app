import org.flywaydb.gradle.task.FlywayMigrateTask
import uk.gov.hmcts.rse.AuthMode

import javax.inject.Inject
import java.nio.charset.StandardCharsets

buildscript {
    dependencies {
        classpath("org.flywaydb:flyway-database-postgresql:11.20.0")
    }
}

plugins {
    id 'application'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.springframework.boot' version '3.5.9'
    id 'uk.gov.hmcts.java' version '0.12.67'
    id 'com.github.ben-manes.versions' version '0.53.0'
    id 'org.sonarqube' version '7.2.2.6593'
    id 'jacoco'
    id 'checkstyle'
    id 'au.com.dius.pact' version '4.6.19'
    id 'org.owasp.dependencycheck' version '12.1.6'
    id 'info.solidsoft.pitest' version '1.19.0-rc.2'
    id 'com.github.hmcts.rse-cft-lib' version '0.19.1973'
    id 'org.flywaydb.flyway' version '11.20.0'
    id 'io.freefair.lombok' version '9.1.0'
    id 'net.serenity-bdd.serenity-gradle-plugin' version '5.0.1-FINAL'
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

if (!project.hasProperty('runList')) {
    project.ext.runList = 'main'
}


group = 'uk.gov.hmcts.reform.em.npa'
version = '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    mainClass = 'uk.gov.hmcts.reform.em.npa.Application'
}

def versions = [
        postgresql         : '42.7.8',
        h2                 : '1.4.199',
        serenity           : '5.0.2',
        pact_version       : '4.6.19',
        logging            : '7.0.0',
        fasterXmlJackson   : '2.20.1',
        hibernate          : '6.6.40.Final',
        mapstruct          : '1.6.3',
        springSecurity     : '6.5.7',
        log4JVersion       : '2.25.3',
        jupiter            : '6.0.2',
        poi                : '5.5.1'
]

sourceSets {
    aat {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
    contractTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
}

configurations {
    aatImplementation.extendsFrom(testImplementation)
    aatRuntime.extendsFrom(testRuntimeOnly)
}

dependencyCheck {
    suppressionFile = 'config/owasp/dependency-check-suppressions.xml'
}

configurations.configureEach {
    exclude group: 'com.vaadin.external.google', module: 'android-json'
    exclude group: 'org.glassfish', module: 'jakarta.el'
    exclude group: 'org.springframework.retry', module: 'spring-retry'
    exclude group: 'org.springframework.security', module: 'spring-security-rsa'
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-security'
    exclude group: 'org.codehaus.jettison', module: 'jettison'
}

dependencies {

    def withoutStuff = {
        exclude group: 'com.sun.xml.bind', module: 'jaxb-osgi'
        exclude group: 'uk.gov.hmcts.reform', module: 'java-logging-spring'
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-security'
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-test'
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-actuator'
    }

    implementation group: 'org.springframework.boot', name: 'spring-boot-actuator'
    implementation group: 'org.springframework.boot', name: 'spring-boot-autoconfigure'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'


    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: '4.3.1'
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '4.3.1'

    implementation group: 'com.google.guava', name: 'guava', version: '33.5.0-jre'

    implementation group: 'org.projectlombok', name: 'lombok', version: '1.18.42'
    implementation group: 'org.apache.pdfbox', name: 'pdfbox', version: '3.0.6'
    implementation group: 'org.apache.pdfbox', name: 'pdfbox-io', version: '3.0.6'
    implementation group: 'org.apache.pdfbox', name: 'jbig2-imageio', version: '3.0.4'

    implementation group: 'com.itextpdf', name: 'cleanup', version: '5.0.5'
    implementation group: 'com.itextpdf', name: 'bouncy-castle-adapter', version: '9.4.0'

    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '5.3.2'

    implementation group:'org.apache.logging.log4j', name: 'log4j-core', version: versions.log4JVersion
    implementation group:'org.apache.logging.log4j', name: 'log4j-api', version: versions.log4JVersion

    implementation group:'com.fasterxml.jackson.datatype', name: 'jackson-datatype-json-org', version: versions.fasterXmlJackson
    implementation group:'com.fasterxml.jackson.module', name: 'jackson-module-blackbird', version: versions.fasterXmlJackson

    implementation group: 'org.zalando', name: 'problem-spring-web', version: '0.29.1'
    implementation group: 'org.zalando', name: 'jackson-datatype-problem', version: '0.27.1'
    implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.8.15'

    implementation group: 'org.springframework.security', name: 'spring-security-oauth2-resource-server', version: versions.springSecurity
    implementation group: 'org.springframework.security', name: 'spring-security-oauth2-client', version: versions.springSecurity
    implementation group: 'org.springframework.security', name: 'spring-security-oauth2-jose', version: versions.springSecurity
    implementation group: 'org.springframework.security', name: 'spring-security-core', version: versions.springSecurity
    implementation group: 'org.springframework.security', name: 'spring-security-config', version: versions.springSecurity
    implementation group: 'org.springframework.security', name: 'spring-security-web', version: versions.springSecurity

    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.14'
    implementation group: 'org.flywaydb', name: 'flyway-core', version: '11.20.0'
    runtimeOnly group: 'org.flywaydb', name: 'flyway-database-postgresql', version: '11.20.0'

    implementation group: 'org.postgresql', name: 'postgresql', version: versions.postgresql
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.20.0'
    implementation group: 'commons-beanutils', name: 'commons-beanutils', version: '1.11.0'
    implementation group: 'commons-validator', name: 'commons-validator', version: '1.10.1'
    implementation group: 'org.ehcache', name: 'ehcache', version: '3.11.1'

    implementation group:'org.hibernate.orm', name: 'hibernate-core', version: versions.hibernate
    implementation group:'org.hibernate.orm', name: 'hibernate-jcache', version: versions.hibernate
    implementation group:'org.hibernate.orm', name: 'hibernate-envers', version: versions.hibernate

    implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '5.3.3'
    implementation group: 'com.github.hmcts', name: 'idam-java-client', version: '3.0.5'
    implementation 'com.github.hmcts:ccd-case-document-am-client:1.59.2'
    implementation group: 'org.apache.tika', name: 'tika-core', version: '3.2.3'

    implementation group: 'org.mapstruct', name: 'mapstruct', version: versions.mapstruct

    annotationProcessor group: 'org.mapstruct',name: 'mapstruct-processor', version: versions.mapstruct
    annotationProcessor group:'org.hibernate.orm', name: 'hibernate-jpamodelgen', version: versions.hibernate
    annotationProcessor group:'org.springframework.boot', name: 'spring-boot-configuration-processor', withoutStuff

    testImplementation group:'org.springframework.boot', name: 'spring-boot-starter-test', withoutStuff
    testImplementation group: 'com.github.gmazzo.okhttp.mock', name: 'mock-client', version: '2.1.0'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '3.0'
    implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: versions.logging

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.jupiter
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.jupiter

    aatImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.jupiter
    aatRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.jupiter


    testImplementation group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity, withoutStuff
    testImplementation group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity, withoutStuff
    testImplementation group: 'net.serenity-bdd', name: 'serenity-junit5', version: versions.serenity
    testImplementation group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity
    testImplementation group: 'net.serenity-bdd', name: 'serenity-single-page-report', version: versions.serenity

    testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.5.24'
    testImplementation group: 'ch.qos.logback', name: 'logback-core', version: '1.5.24'

    testImplementation group: 'com.github.hmcts', name: 'fortify-client', version: '1.4.10', classifier: 'all', {
        exclude group: 'commons-io', module: 'commons-io'
        exclude group: 'org.apache.commons', module: 'commons-lang3'
        exclude group: 'ch.qos.logback', module: 'logback-classic'
        exclude group: 'ch.qos.logback', module: 'logback-core'
    }

    testImplementation group: 'org.testcontainers', name: 'postgresql', version: '1.21.4'

    contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit5',  version: versions.pact_version

    contractTestImplementation group: 'au.com.dius.pact.provider', name: 'junit5', version: versions.pact_version
    contractTestImplementation group: 'au.com.dius.pact.provider', name: 'spring', version: versions.pact_version
    contractTestImplementation group: 'au.com.dius.pact.provider', name: 'junit5spring', version: versions.pact_version
    contractTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'


    contractTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.jupiter
    contractTestRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.jupiter

    contractTestImplementation sourceSets.main.runtimeClasspath
    contractTestImplementation sourceSets.test.runtimeClasspath

    contractTestImplementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.14'
    contractTestImplementation group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.14'
    contractTestImplementation group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.14'

    aatImplementation group: 'com.github.hmcts', name: 'em-test-helper', version:'2.3.0'
    aatImplementation group: 'org.apache.poi', name: 'poi', version: versions.poi
    aatImplementation group: 'org.apache.poi', name: 'poi-ooxml', version: versions.poi
    cftlibImplementation 'org.springframework.boot:spring-boot-devtools'
}

dependencyManagement {
    dependencies {
        //CVE-2022-23437
        dependencySet(group: 'xerces', version: '2.12.2') {
            entry 'xercesImpl'
        }
        //CVE-2020-13956
        dependencySet(group: 'org.apache.httpcomponents', version: '4.5.14') {
            entry 'fluent-hc'
        }
        dependencySet(group: 'org.junit.platform', version: '6.0.2') {
            entry 'junit-platform-commons'
            entry 'junit-platform-engine'
            entry 'junit-platform-launcher'
        }
    }
}

bootJar {
    getArchiveFileName().set(provider {
        'rpa-native-pdf-annotator-app.jar'
    })
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

def coverageExclusionList = [
    '**uk/gov/hmcts/reform/em/npa/Application*',
    '**uk/gov/hmcts/reform/em/npa/appinsights/*',
    '**uk/gov/hmcts/reform/em/npa/config/**/*',
    '**uk/gov/hmcts/reform/em/npa/data/migration/PendingMigrationScriptException*',
    '**uk/gov/hmcts/reform/em/npa/data/migration/FlywayNoOpStrategy*',
    '**uk/gov/hmcts/reform/em/npa/domain/*',
    '**uk/gov/hmcts/reform/em/npa/service/dto/external/annotation/*',
    '**uk/gov/hmcts/reform/em/npa/ccd/domain/*',
    '**uk/gov/hmcts/reform/em/npa/ccd/dto/*',
    '**uk/gov/hmcts/reform/em/npa/ccd/exception/*',
    '**uk/gov/hmcts/reform/em/npa/service/exception/*',
    '**uk/gov/hmcts/reform/em/npa/service/dto/redaction/*',
    '**uk/gov/hmcts/reform/em/npa/info/*',
    '**uk/gov/hmcts/reform/em/npa/rest/errors/ExceptionTranslator*',
    '**uk/gov/hmcts/reform/em/npa/service/dto/AbstractAuditingDTO*',
    '**uk/gov/hmcts/reform/em/npa/rest/ResponseUtil*',
    '**uk/gov/hmcts/reform/em/npa/config/FlywayConfiguration*',
    '**uk/gov/hmcts/reform/em/npa/batch/RemoveLargeObjectsTasklet*',
    '**uk/gov/hmcts/reform/em/npa/repository/LargeObjectRepository*'
]

checkstyle {
    maxWarnings = 0
    toolVersion = '13.0.0'
    // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
    getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

checkstyleMain.shouldRunAfter(compileJava)
test.shouldRunAfter(checkstyleTest)

jacocoTestReport {
    executionData(test)

    reports {
        xml.required = true
        csv.required = false
        xml.outputLocation = layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml")
    }

}

project.tasks['sonarqube'].dependsOn test, jacocoTestReport

sonarqube {
    properties {
        property "sonar.projectName", "${rootProject.name}"
        property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.outputLocation}"
        property "sonar.exclusions", coverageExclusionList.join(", ")
        property "sonar.test.exclusions", "**/test/**"
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"

        property "sonar.issue.ignore.multicriteria", "e1"
        property "sonar.issue.ignore.multicriteria.e1.ruleKey", "java:S2629"
        property "sonar.issue.ignore.multicriteria.e1.resourceKey", "src/main/java/uk/gov/hmcts/reform/em/npa/**"
    }
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
    javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

test {
    useJUnitPlatform()
}

tasks.register('functional', Test) {
    group = 'Delivery pipeline'
    description = 'Executes functional tests'
    useJUnitPlatform()
    setTestClassesDirs(sourceSets.aat.output.classesDirs)
    setClasspath(sourceSets.aat.runtimeClasspath)
    include "uk/gov/hmcts/reform/em/npa/functional/**"

    // Serenity Single page Reports
    // functional.finalizedBy(aggregate)

    environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
}

tasks.register('smoke', Test) {
    group = 'Delivery pipeline'
    description = 'Executes non-destructive smoke tests'
    setTestClassesDirs(sourceSets.aat.output.classesDirs)
    setClasspath(sourceSets.aat.runtimeClasspath)
    useJUnitPlatform()
    include "uk/gov/hmcts/reform/em/npa/smoke/**"

    environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
}

project.ext {
    pacticipantVersion = getCheckedOutGitCommitHash()
}

static def getCheckedOutGitCommitHash() {
    'git rev-parse --verify --short HEAD'.execute().text.trim()
}


tasks.register('contract', Test) {
    group = 'Delivery pipeline'
    description = 'Runs the consumer Pact tests'
    useJUnitPlatform()
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
    include '**/consumer/**'
    systemProperty 'pact.rootDir', "pacts"
    environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
}


pact {
    broker {
        pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
    }
    publish {
        pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
        pactDirectory = 'pacts'
        tags = [System.getenv("PACT_BRANCH_NAME") ?: 'Dev']
        consumerBranch = System.getenv("PACT_BRANCH_NAME") ?: 'Dev'
        version = project.pacticipantVersion
    }
}

pactPublish {
    pactPublish = project.pact.publish
}

tasks.register('runAndPublishConsumerPactTests', Test) {
    logger.lifecycle("Runs pact Tests")
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
    jvmArgs = ['--add-opens=java.base/java.lang=ALL-UNNAMED']
    include '**/consumer/**'
    useJUnitPlatform()
}

runAndPublishConsumerPactTests.dependsOn contract
runAndPublishConsumerPactTests.finalizedBy pactPublish

tasks.register('providerContractTests', Test) {
    description = 'Runs ONLY the provider Pact verification tests.'
    group = 'Verification'

    useJUnitPlatform()
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath

    include '**/provider/**'
}

// Centralized Pact system property setup for all Test tasks
tasks.withType(Test).configureEach { t ->
    // Logging setup
    testLogging {
        exceptionFormat = 'full'
        showCauses = true
        showExceptions = true
        showStackTraces = true
        showStandardStreams = true
        events "passed", "skipped", "failed", "standard_out", "standard_error"
    }

    if (project.hasProperty('pact.verifier.publishResults')) {
        systemProperty 'pact.verifier.publishResults', project.property('pact.verifier.publishResults')
    }
    if (project.hasProperty('pact.provider.version')) {
        systemProperty 'pact.provider.version', project.property('pact.provider.version')
    } else {
        systemProperty 'pact.provider.version', project.pacticipantVersion
    }

    if (project.hasProperty('pact.provider.branch')) {
        systemProperty 'pact.provider.branch', project.property('pact.provider.branch')
    } else {
        systemProperty 'pact.provider.branch', System.getenv("PACT_BRANCH_NAME") ?: "Dev"
    }

    doFirst {
        println "--- Running '${t.name}' ---"
        println "System properties for this run: ${systemProperties}"
    }
}

tasks.register('runProviderPactVerification') {
    description = 'Entry point for Jenkins to run provider contract tests.'
    group = 'Verification'

    dependsOn tasks.named('providerContractTests')
}

tasks.register('fortifyScan', JavaExec) {
    mainClass.set("uk.gov.hmcts.fortifyclient.FortifyClientMainApp")
    classpath += sourceSets.test.runtimeClasspath
    jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

serenity {
    reports = ["single-page-html"]
}

compileJava.dependsOn processResources

pitest {

    logger.lifecycle("Runs PIT  Tests")
    targetClasses = ['uk.gov.hmcts.reform.em.npa.*']

    excludedClasses = [
            'uk.gov.hmcts.reform.em.npa.appinsights.*' ,
            'uk.gov.hmcts.reform.em.npa.domain.*' ,
            'uk.gov.hmcts.reform.em.npa.info.*',
            'uk.gov.hmcts.reform.em.npa.Application.java',
            'uk.gov.hmcts.reform.em.npa.infrastructure.*'

    ]
    enableDefaultIncrementalAnalysis = true
    historyInputLocation = 'build/reports/pitest/fastermutationtesting'
    historyOutputLocation = 'build/reports/pitest/fastermutationtestingoutput'
    threads = 15
    testSourceSets = [sourceSets.test]
    mainSourceSets = [sourceSets.main]
    fileExtensionsToFilter.addAll('xml','json')
    outputFormats = ['XML', 'HTML','CSV']
    coverageThreshold = 0
    jvmArgs = ['-Xms1G','-Xmx3G']
    timestampedReports = false
    failWhenNoMutations = false
    detectInlinedCode = true
    threads = 10
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    failWhenNoMutations = false
}

project.tasks['pitest'].group = "Verification"

interface ExecOperationsProvider {
    @Inject
    ExecOperations getExecOperations()
}


tasks.register('loadEnvSecrets') {
    doLast {
        if (!project.file('./.aat-env').exists()) {
            def execOps = project.objects.newInstance(ExecOperationsProvider).getExecOperations()

            new ByteArrayOutputStream().withStream { os ->
                execOps.exec {
                    commandLine 'az', 'keyvault', 'secret', 'show', '--vault-name', 'em-npa-aat', '-o', 'tsv', '--query', 'value', '--name', 'em-npa-dot-env'
                    standardOutput = os
                }
                project.file('./.aat-env').write(new String(os.toString().replace('\n', '').decodeBase64(), StandardCharsets.UTF_8))
            }
        }
    }
}

tasks.register('reloadEnvSecrets') {
    dependsOn(loadEnvSecrets)
    doFirst {
        if (project.file('./.aat-env').exists()) {
            project.file('./.aat-env').delete()
        }
    }
}

bootRun {
    doFirst() {
        if (project.file('./.aat-env').exists()) {
            project.file('./.aat-env').readLines().each() {
                def index = it.indexOf("=")
                def key = it.substring(0, index)
                def value = it.substring(index + 1)
                environment key, value
            }
        }
    }
}

functional {
    doFirst() {
        if (project.file('./.aat-env').exists()) {
            project.file('./.aat-env').readLines().each() {
                def index = it.indexOf("=")
                def key = it.substring(0, index)
                def value = it.substring(index + 1)
                environment key, value
            }
        }
    }
}

bootWithCCD {
    dependsOn(loadEnvSecrets)

    doFirst() {
        project.file('./.aat-env').readLines().each() {
            def index = it.indexOf("=")
            def key = it.substring(0, index)
            def value = it.substring(index + 1)
            environment key, value
        }

        def execOps = project.objects.newInstance(ExecOperationsProvider).getExecOperations()


        execOps.exec {
            commandLine 'docker', 'compose', '-f', 'src/cftlib/resources/docker-compose-local.yml', '-p', 'cftlib', 'up', '-d'
        }
    }
    environment 'RSE_LIB_ADDITIONAL_DATABASES', 'emnpa,evidence'
    environment 'DATA_STORE_S2S_AUTHORISED_SERVICES', 'ccd_gw,ccd_data,ccd_ps,aac_manage_case_assignment,ccd_case_document_am_api,am_role_assignment_service,hmc_cft_hearing_service,prl_citizen_frontend,xui_webapp'
    environment 'S2S_URL', 'http://rpe-service-auth-provider-aat.service.core-compute-aat.internal'
    environment 'CASE_DOCUMENT_AM_API_S2S_SECRET','${CASE_DOCUMENT_AM_API_S2S_SECRET}'
    environment 'DOC_ASSEMBLY_SERVER_PORT', '8081'
    authMode = AuthMode.Local
}

flyway {
    url = System.getenv('FLYWAY_URL')
    user = System.getenv('FLYWAY_USER')
    password = System.getenv('FLYWAY_PASSWORD')
    baselineOnMigrate = true
    baselineVersion = '1'
}

tasks.register('migratePostgresDatabase', FlywayMigrateTask) {
    baselineOnMigrate = true
    if (project.hasProperty("dburl")) {
        url = "jdbc:postgresql://${dburl}"
    }
}

// exclude the logback binding is coming in via cftlib
configurations.cftlibTestImplementation {
    exclude group: 'ch.qos.logback', module: 'logback-classic'
}

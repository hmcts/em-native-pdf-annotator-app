buildscript {
    dependencies {
        classpath("net.serenity-bdd:serenity-gradle-plugin:2.3.13")
        classpath("net.serenity-bdd:serenity-single-page-report:2.3.13")
    }
}

plugins {
    id 'application'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.springframework.boot' version '2.3.4.RELEASE'
    id 'uk.gov.hmcts.java' version '0.12.5'
    id 'com.github.ben-manes.versions' version '0.36.0'
    id 'org.sonarqube' version '3.1.1'
    id 'jacoco'
    id 'org.liquibase.gradle' version '2.0.4'
    id 'au.com.dius.pact' version '4.1.8'
    id 'io.franzbecker.gradle-lombok' version '4.0.0'
    id "info.solidsoft.pitest" version '1.5.2'
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.springframework.boot'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'idea'
apply plugin: 'net.serenity-bdd.aggregator'

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

if (!project.hasProperty('runList')) {
    project.ext.runList = 'main'
}

//loading properties file.
Properties liquibaseProps = new Properties()
liquibaseProps.load(new FileInputStream("$projectDir/src/main/resources/liquibase.properties"))

project.ext.diffChangelogFile = 'src/main/resources/db/changelog/' + new Date().format('yyyyMMddHHmmss') + '_changelog.xml'

liquibase {
    activities {
        main {
            driver liquibaseProps.getProperty('driver')
            url project.hasProperty("dburl") ? "jdbc:postgresql://$dburl" : liquibaseProps.getProperty('url')
            username project.hasProperty("flyway.user") ? "${rootProject.properties['flyway.user']}" : liquibaseProps.getProperty('username')
            password project.hasProperty("flyway.password") ? "${rootProject.properties['flyway.password']}" : liquibaseProps.getProperty('password')
            changeLogFile liquibaseProps.getProperty('changeLogFile')
            referenceUrl liquibaseProps.getProperty('referenceUrl')
            defaultSchemaName ''
            logLevel 'debug'
            classpath 'src/main/resources/'
        }
        diffLog {
            driver liquibaseProps.getProperty('driver')
            url project.hasProperty("dburl") ? "jdbc:postgresql://$dburl" : liquibaseProps.getProperty('url')
            username project.hasProperty("flyway.user") ? "${rootProject.properties['flyway.user']}" : liquibaseProps.getProperty('username')
            password project.hasProperty("flyway.password") ? "${rootProject.properties['flyway.password']}" : liquibaseProps.getProperty('password')
            referenceUrl liquibaseProps.getProperty('referenceUrl')
            defaultSchemaName ''
            logLevel 'debug'
            classpath 'src/main/resources/'
        }
    }

    runList = project.ext.runList
}

liquibaseDiff.dependsOn compileJava
liquibaseDiffChangeLog.dependsOn compileJava

group 'uk.gov.hmcts.reform.em.npa'
version '1.0.0'
sourceCompatibility = 11
targetCompatibility = 11
mainClassName = 'uk.gov.hmcts.reform.em.npa.Application'

configurations {
    aatCompile.extendsFrom(testCompile)
    aatRuntime.extendsFrom(testRuntime)
}

repositories {
    maven { url "https://dl.bintray.com/hmcts/hmcts-maven" }
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
    jcenter()
    mavenLocal()
    maven {
        url 'https://repo.spring.io/libs-snapshot'
    }
}

def versions = [
        postgresql         : '42.2.14',
        springfoxSwagger   : '2.9.2',
        h2                 : '1.4.199',
        liquibase          : '3.8.0',
        serenity           : '2.3.13',
        serenityreporter   : '2.3.13',
        serenityRestAssured: '2.3.4',
        pact_version       : '4.0.10',
        logging            : '5.1.7',
        gradlePitest       : '1.3.0',
        sonarPitest        : '0.5',
        fasterXmlJackson   : '2.11.3',
        hibernate          : '5.4.25.Final',
        springSecurity     : '5.3.4.RELEASE'
]

sourceSets {
    aat {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir('src/aat/java')
        }
        resources {
            srcDir('src/aat/resources')
        }
    }
    contractTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/contractTest/java')
        }
        resources.srcDir file('src/contactTest/resources')
    }
}

dependencyCheck {
    suppressionFile = 'config/owasp/dependency-check-suppressions.xml'
}

configurations.all {
    resolutionStrategy {
        eachDependency { DependencyResolveDetails details ->
            if (details.requested.group in ['org.apache.tomcat.embed']) {
                details.useVersion '9.0.40'
            }
            if (details.requested.group in ['org.springframework']) {
                details.useVersion '5.2.9.RELEASE'
            }
            if (details.requested.group in ['org.apache.logging.log4j']) {
                details.useVersion '2.13.2'
            }
            if (details.requested.name in ['snakeyaml']) {
                details.useVersion '1.26'
            }
            if (details.requested.name in ['spring-boot-actuator, spring-boot-actuator-autoconfigure, ' +
                                                   'spring-boot-autoconfigure, spring-data-commons']) {
                details.useVersion '2.3.4.RELEASE'
            }
        }
    }
}

dependencies {

    def withoutStuff = {
        exclude group: 'com.vaadin.external.google', module: 'android-json'
        exclude group: 'com.sun.xml.bind', module: 'jaxb-osgi'
        exclude group: 'uk.gov.hmcts.reform', module: 'java-logging-spring'
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-security'
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-test'
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-actuator'
    }

    def withoutNetty = {
        exclude group: 'io.netty', module: 'netty-handler'
        exclude group: 'io.netty', module: 'netty-codec-http2'
        exclude group: 'io.netty', module: 'netty-transport-native-kqueue'
        exclude group: 'io.netty', module: 'netty-transport-native-epoll'
        exclude group: 'org.eclipse.jetty.alpn', module: 'alpn-api'
    }

    compile group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: '10.0.0'
    compile group: 'com.google.guava', name: 'guava', version: '30.1-jre'
    
    compile ('org.springframework.cloud:spring-cloud-openfeign:3.0.0') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-actuator'
    }

    compile 'org.projectlombok:lombok:1.18.18'
    compile group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.22'
    compile group: 'org.apache.pdfbox', name: 'jbig2-imageio', version: '3.0.3'
    compile 'com.squareup.okhttp3:okhttp:4.9.1'
    compile("org.springframework.boot:spring-boot-starter-batch:2.3.4.RELEASE")
    compile "org.springframework.boot:spring-boot-actuator:2.3.4.RELEASE"
    compile("org.springframework.boot:spring-boot-autoconfigure:2.3.4.RELEASE")
    compile("org.springframework.boot:spring-boot:2.3.4.RELEASE")
    compile 'org.springframework.boot:spring-boot-starter-validation:2.3.4.RELEASE'

    compile group:'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.0'

    compile group:'com.fasterxml.jackson.datatype', name: 'jackson-datatype-json-org', version: versions.fasterXmlJackson
    compile group:'com.fasterxml.jackson.datatype', name: 'jackson-datatype-hppc', version: versions.fasterXmlJackson
    compile group:'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: versions.fasterXmlJackson
    compile group:'com.fasterxml.jackson.datatype', name: 'jackson-datatype-hibernate5', version: versions.fasterXmlJackson
    compile group:'com.fasterxml.jackson.core', name: 'jackson-annotations', version: versions.fasterXmlJackson
    compile group:'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.fasterXmlJackson
    compile group:'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.fasterXmlJackson
    compile group:'com.fasterxml.jackson.module', name: 'jackson-module-afterburner', version: versions.fasterXmlJackson

    compile "org.zalando:problem-spring-web:0.26.2"
    compile("io.springfox:springfox-swagger2:${versions.springfoxSwagger}")
    compile("io.springfox:springfox-swagger-ui:${versions.springfoxSwagger}")
    compile("org.springframework.boot:spring-boot-starter-web:2.3.4.RELEASE")
    compile('org.springframework.boot:spring-boot-starter-data-jpa:2.3.4.RELEASE')

    compile group: 'org.springframework.security', name: 'spring-security-oauth2-resource-server', version: versions.springSecurity
    compile group: 'org.springframework.security', name: 'spring-security-oauth2-client', version: versions.springSecurity
    compile group: 'org.springframework.security', name: 'spring-security-web', version: versions.springSecurity
    compile group: 'org.springframework.security', name: 'spring-security-oauth2-jose', version: versions.springSecurity
    compile group: 'org.springframework.security', name: 'spring-security-oauth2-core', version: versions.springSecurity
    compile group: 'org.springframework.security', name: 'spring-security-config', version: versions.springSecurity
    compile group: 'org.springframework.security', name: 'spring-security-core', version: versions.springSecurity

    compile( 'org.apache.httpcomponents:httpclient:4.5.10')
    compile "org.liquibase:liquibase-core:4.3.0"
    liquibaseRuntime "org.liquibase:liquibase-core:4.3.0"
    liquibaseRuntime "org.liquibase.ext:liquibase-hibernate5:4.3.1"
    liquibaseRuntime sourceSets.main.compileClasspath
    liquibaseRuntime sourceSets.main.output
    liquibaseRuntime "org.liquibase:liquibase-groovy-dsl:3.0.0"
    liquibaseRuntime group: 'org.postgresql', name: 'postgresql', version: versions.postgresql
    liquibaseRuntime "com.h2database:h2"
    compile group: 'org.postgresql', name: 'postgresql', version: versions.postgresql
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'
    compile group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'
    compile group: 'commons-validator', name: 'commons-validator', version: '1.7'
    compile "javax.transaction:javax.transaction-api"
    compile group: 'org.ehcache', name: 'ehcache', version: '3.9.1'

    compile "com.zaxxer:HikariCP:4.0.0"
    compile group: 'javax.el', name: 'javax.el-api', version: '3.0.0'
    compile group:'org.hibernate', name: 'hibernate-core', version: versions.hibernate
    compile group:'org.hibernate', name: 'hibernate-jcache', version: versions.hibernate
    compile group:'org.hibernate', name: 'hibernate-entitymanager', version: versions.hibernate
    compile group:'org.hibernate', name: 'hibernate-envers', version: versions.hibernate
    
    compile group: 'uk.gov.hmcts.reform.auth', name: 'auth-checker-lib', version: '2.1.4'
    compile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '3.1.4'
    
    compile group: 'uk.gov.hmcts.reform', name: 'idam-client', version: '1.5.5'

    compile group: 'org.apache.tika', name: 'tika-core', version: '1.25'

    compile "org.mapstruct:mapstruct-jdk8:${mapstruct_version}"

    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
    compile group: 'org.apache.httpcomponents.client5', name: 'httpclient5', version: '5.0.3'
    compile group: 'org.apache.httpcomponents.core5', name: 'httpcore5', version: '5.0.3'
    compile group: 'org.apache.httpcomponents.core5', name: 'httpcore5-h2', version: '5.0.3'
    compile group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.13'
    compile group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.13'

    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstruct_version}"
    annotationProcessor group:'org.hibernate', name: 'hibernate-jpamodelgen', version: versions.hibernate
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:2.3.4.RELEASE", withoutStuff

    testCompile "org.springframework.boot:spring-boot-starter-test:2.3.4.RELEASE", withoutStuff
    testCompile group: 'com.h2database', name: 'h2', version: '1.4.200'
    testCompile 'com.github.gmazzo:okhttp-mock:1.3.2'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    compile group: 'uk.gov.hmcts.reform', name: 'logging', version: versions.logging
    compile group: 'uk.gov.hmcts.reform', name: 'logging-spring', version: versions.logging
    compile group: 'uk.gov.hmcts.reform', name: 'logging-appinsights', version: versions.logging

    compile group: 'uk.gov.hmcts.reform', name: 'properties-volume-spring-boot-starter', version:'0.1.0'

    annotationProcessor 'javax.xml.bind:jaxb-api:2.3.1'
    annotationProcessor 'com.sun.xml.bind:jaxb-core:3.0.0'
    annotationProcessor 'com.sun.xml.bind:jaxb-impl:3.0.0'

    testCompile "org.junit.jupiter:junit-jupiter-api:5.6.3"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:5.6.3"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.3"

    testCompile group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenityRestAssured, withoutStuff
    testCompile group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity, withoutStuff
    testCompile group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenity
    testCompile group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity
    testCompile group: 'net.serenity-bdd', name: 'serenity-single-page-report', version: versions.serenityreporter

    contractTestCompile group: 'au.com.dius', name: 'pact-jvm-consumer-junit', version: versions.pact_version, withoutNetty
    contractTestCompile group: 'au.com.dius', name: 'pact-jvm-consumer-junit5', version: versions.pact_version, withoutNetty
    contractTestCompile group: 'au.com.dius', name: 'pact-jvm-consumer-java8', version: versions.pact_version, withoutNetty
    compile group: 'au.com.dius', name: 'pact-jvm-consumer', version: versions.pact_version, withoutNetty

    contractTestCompile("org.junit.jupiter:junit-jupiter-api:5.6.3")
    contractTestRuntime("org.junit.jupiter:junit-jupiter-engine:5.6.3")
    contractTestImplementation('org.junit.jupiter:junit-jupiter-api:5.6.3')

    contractTestCompile sourceSets.main.runtimeClasspath
    contractTestCompile sourceSets.test.runtimeClasspath

    contractTestCompile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
    contractTestCompile group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.13'
    contractTestCompile group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.13'

    aatCompile ("uk.gov.hmcts.reform:em-test-helper:1.18.1") {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-test'
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-actuator'
    }
    aatCompile group: 'org.apache.poi', name: 'poi', version: '4.1.2'
    aatCompile group: 'org.apache.poi', name: 'poi-ooxml', version: '5.0.0'

}

dependencyManagement {
    dependencies {

        dependencySet(group: 'org.jetbrains.kotlin', version: '1.4.30') {
            entry 'kotlin'
            entry 'kotlin-stdlib'
            entry 'kotlin-reflect'
            entry 'kotlin-stdlib-common'
            entry 'kotlin-stdlib-jdk7'
            entry 'kotlin-stdlib-jdk8'
        }
    }
}

bootJar {
    baseName 'rpa-native-pdf-annotator-app'
    archiveName = "$baseName"+".jar"
    destinationDir = file("$rootDir/build/libs")
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

def coverageExclusionList = [
    '**uk/gov/hmcts/reform/em/npa/Application*',
    '**uk/gov/hmcts/reform/em/npa/appinsights/*',
    '**uk/gov/hmcts/reform/em/npa/config/*',
    '**uk/gov/hmcts/reform/em/npa/config/logging/*',
    '**uk/gov/hmcts/reform/em/npa/domain/*',
    '**uk/gov/hmcts/reform/em/npa/service/dto/external/annotation/*',
    '**uk/gov/hmcts/reform/em/npa/config/security/*',
    '**uk/gov/hmcts/reform/em/npa/ccd/domain/*',
    '**uk/gov/hmcts/reform/em/npa/ccd/dto/*',
    '**uk/gov/hmcts/reform/em/npa/ccd/exception/*',
    '**uk/gov/hmcts/reform/em/npa/service/exception/*',
    '**uk/gov/hmcts/reform/em/npa/service/dto/redaction/*'
]

jacocoTestReport {
    executionData(test)

    reports {
        xml.enabled = true
        csv.enabled = true

        xml.destination file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }

}

project.tasks['sonarqube'].dependsOn jacocoTestReport

sonarqube {
    properties {
        property "sonar.projectName", "${rootProject.name}"
        property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.destination.path}"
        property "sonar.exclusions", coverageExclusionList.join(", ")
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    }
}

idea {
    module {
        testSourceDirs += project.sourceSets.aat.java.srcDirs
        testResourceDirs += project.sourceSets.aat.resources.srcDirs
        testSourceDirs += project.sourceSets.contractTest.java.srcDirs
        testResourceDirs += project.sourceSets.contractTest.resources.srcDirs
    }
}

task functional(type: Test) {
    group = 'Delivery pipeline'
    description = 'Executes functional tests'
    setTestClassesDirs(sourceSets.aat.output.classesDirs)
    setClasspath(sourceSets.aat.runtimeClasspath)
    include "uk/gov/hmcts/reform/em/npa/functional/**"

    // Serenity Single page Reports
    functional.finalizedBy(aggregate)

    environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
}

task smoke(type: Test) {
    group = 'Delivery pipeline'
    description = 'Executes non-destructive smoke tests'
    setTestClassesDirs(sourceSets.aat.output.classesDirs)
    setClasspath(sourceSets.aat.runtimeClasspath)
    include "uk/gov/hmcts/reform/em/npa/smoke/**"

    environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
}

task migratePostgresDatabase(dependsOn: 'liquibaseUpdate')


task contract(type: Test) {
    group = 'Delivery pipeline'
    description = 'Runs the consumer Pact tests'
    useJUnitPlatform()
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
    include "uk/gov/hmcts/reform/em/npa/**"
    systemProperty 'pact.rootDir', "pacts"
    environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
}

task runAndPublishConsumerPactTests(type: Test){
    logger.lifecycle("Runs pact Tests")
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath

}

runAndPublishConsumerPactTests.dependsOn contract

runAndPublishConsumerPactTests.finalizedBy pactPublish


project.ext {
    pactVersion = getCheckedOutGitCommitHash()
}

def getCheckedOutGitCommitHash() {
    'git rev-parse --verify --short HEAD'.execute().text.trim()
}

serenity {
    reports = ["single-page-html"]
}

pact {
    publish {
        pactDirectory = 'pacts'
        pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'https://localhost:80'
        version = project.pactVersion
    }
}

checkstyle {
//    ignoreFailures = true
    sourceSets = []
}

compileJava.dependsOn processResources

pitest {

    logger.lifecycle("Runs PIT  Tests")

    targetClasses = ['uk.gov.hmcts.reform.em.npa.*']

    excludedClasses = [
            'uk.gov.hmcts.reform.em.npa.appinsights.*' ,
            'uk.gov.hmcts.reform.em.npa.domain.*' ,
            'uk.gov.hmcts.reform.em.npa.info.*',
            'uk.gov.hmcts.reform.em.npa.Application.java'
    ]

    enableDefaultIncrementalAnalysis = true
    historyInputLocation = 'build/reports/pitest/fastermutationtesting'
    historyOutputLocation = 'build/reports/pitest/fastermutationtestingoutput'
    threads = 15
    testSourceSets = [sourceSets.test]
    mainSourceSets = [sourceSets.main]
    fileExtensionsToFilter.addAll('xml','json')
    outputFormats = ['XML', 'HTML','CSV']
    mutationThreshold = 5
    coverageThreshold = 0
    maxMutationsPerClass = 15
    jvmArgs = ['-Xms1G','-Xmx3G']
    timestampedReports = false
    failWhenNoMutations = false
    detectInlinedCode = true

}

project.tasks['pitest'].group = "Verification"

buildscript {
    dependencies {
        classpath("net.serenity-bdd:serenity-gradle-plugin:2.6.0")
        classpath("net.serenity-bdd:serenity-single-page-report:2.6.0")
    }
}

plugins {
    id 'application'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.springframework.boot' version '2.5.5'
    id 'uk.gov.hmcts.java' version '0.12.16'
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'org.sonarqube' version '3.3'
    id 'jacoco'
    id 'checkstyle'
    id 'org.liquibase.gradle' version '2.1.0'
    id 'au.com.dius.pact' version '4.2.14'
    id 'io.franzbecker.gradle-lombok' version '5.0.0'
    id "info.solidsoft.pitest" version '1.5.2'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'net.serenity-bdd.aggregator'

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

if (!project.hasProperty('runList')) {
    project.ext.runList = 'main'
}

//loading properties file.
Properties liquibaseProps = new Properties()
liquibaseProps.load(new FileInputStream("$projectDir/src/main/resources/liquibase.properties"))

project.ext.diffChangelogFile = 'src/main/resources/db/changelog/' + new Date().format('yyyyMMddHHmmss') + '_changelog.xml'

liquibase {
    activities {
        main {
            driver liquibaseProps.getProperty('driver')
            url project.hasProperty("dburl") ? "jdbc:postgresql://$dburl" : liquibaseProps.getProperty('url')
            username project.hasProperty("flyway.user") ? "${rootProject.properties['flyway.user']}" : liquibaseProps.getProperty('username')
            password project.hasProperty("flyway.password") ? "${rootProject.properties['flyway.password']}" : liquibaseProps.getProperty('password')
            changeLogFile liquibaseProps.getProperty('changeLogFile')
            referenceUrl liquibaseProps.getProperty('referenceUrl')
            defaultSchemaName ''
            logLevel 'debug'
            classpath 'src/main/resources/'
        }
        diffLog {
            driver liquibaseProps.getProperty('driver')
            url project.hasProperty("dburl") ? "jdbc:postgresql://$dburl" : liquibaseProps.getProperty('url')
            username project.hasProperty("flyway.user") ? "${rootProject.properties['flyway.user']}" : liquibaseProps.getProperty('username')
            password project.hasProperty("flyway.password") ? "${rootProject.properties['flyway.password']}" : liquibaseProps.getProperty('password')
            referenceUrl liquibaseProps.getProperty('referenceUrl')
            defaultSchemaName ''
            logLevel 'debug'
            classpath 'src/main/resources/'
        }
    }

    runList = project.ext.runList
}

liquibaseDiff.dependsOn compileJava
liquibaseDiffChangeLog.dependsOn compileJava

group 'uk.gov.hmcts.reform.em.npa'
version '1.0.0'
sourceCompatibility = 11
targetCompatibility = 11
mainClassName = 'uk.gov.hmcts.reform.em.npa.Application'

configurations {
    aatImplementation.extendsFrom(testImplementation)
    aatRuntime.extendsFrom(testRuntimeOnly)
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

def versions = [
        postgresql         : '42.2.20',
        springfoxSwagger   : '2.9.2',
        h2                 : '1.4.199',
        liquibase          : '3.8.0',
        serenity           : '2.6.0',
        serenityreporter   : '2.6.0',
        serenityRestAssured: '2.6.0',
        pact_version       : '4.0.10',
        logging            : '5.1.7',
        gradlePitest       : '1.3.0',
        sonarPitest        : '0.5',
        fasterXmlJackson   : '2.13.0',
        hibernate          : '5.6.0.Final',
        mapstruct          : '1.2.0.Final',
        springSecurity     : '5.5.2',
        log4JVersion       : '2.17.0'
]

sourceSets {
    aat {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
    contractTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
}

dependencyCheck {
    suppressionFile = 'config/owasp/dependency-check-suppressions.xml'
}

configurations.all {
    exclude group: 'com.vaadin.external.google',module: 'android-json'
    exclude group: 'org.glassfish', module: 'jakarta.el'
}

dependencies {

    def withoutStuff = {
        exclude group: 'com.sun.xml.bind', module: 'jaxb-osgi'
        exclude group: 'uk.gov.hmcts.reform', module: 'java-logging-spring'
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-security'
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-test'
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-actuator'
    }

    def withoutNetty = {
        exclude group: 'io.netty', module: 'netty-handler'
        exclude group: 'io.netty', module: 'netty-codec-http2'
        exclude group: 'io.netty', module: 'netty-transport-native-kqueue'
        exclude group: 'io.netty', module: 'netty-transport-native-epoll'
        exclude group: 'org.eclipse.jetty.alpn', module: 'alpn-api'
    }

    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-batch'
    implementation group: 'org.springframework.boot', name: 'spring-boot-actuator'
    implementation group: 'org.springframework.boot', name: 'spring-boot-autoconfigure'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'

    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: '3.0.4'

    implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: '9.0.54'
    implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-websocket', version: '9.0.54'
    
    implementation group: 'com.google.guava', name: 'guava', version: '31.0.1-jre'

    implementation group: 'org.springframework.cloud', name: 'spring-cloud-openfeign', version: '3.0.4', {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-actuator'
    }
        
    implementation group: 'org.projectlombok', name: 'lombok', version: '1.18.22'
    implementation group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.24'
    implementation group: 'org.apache.pdfbox', name: 'jbig2-imageio', version: '3.0.3'
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.9.3'

    implementation group:'org.apache.logging.log4j', name: 'log4j-core', version: versions.log4JVersion

    implementation group:'com.fasterxml.jackson.datatype', name: 'jackson-datatype-json-org', version: versions.fasterXmlJackson
    implementation group:'com.fasterxml.jackson.datatype', name: 'jackson-datatype-hppc', version: versions.fasterXmlJackson
    implementation group:'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: versions.fasterXmlJackson
    implementation group:'com.fasterxml.jackson.datatype', name: 'jackson-datatype-hibernate5', version: versions.fasterXmlJackson
    implementation group:'com.fasterxml.jackson.core', name: 'jackson-annotations', version: versions.fasterXmlJackson
    implementation group:'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.fasterXmlJackson
    implementation group:'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.fasterXmlJackson
    implementation group:'com.fasterxml.jackson.module', name: 'jackson-module-afterburner', version: versions.fasterXmlJackson

    implementation group:'org.zalando', name: 'problem-spring-web', version: '0.26.2'
    implementation group:'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
    implementation group:'io.springfox', name: 'springfox-swagger-ui', version: versions.springfoxSwagger

    implementation group: 'org.springframework.security', name: 'spring-security-oauth2-resource-server', version: versions.springSecurity
    implementation group: 'org.springframework.security', name: 'spring-security-oauth2-client', version: versions.springSecurity
    implementation group: 'org.springframework.security', name: 'spring-security-web', version: versions.springSecurity
    implementation group: 'org.springframework.security', name: 'spring-security-oauth2-jose', version: versions.springSecurity
    implementation group: 'org.springframework.security', name: 'spring-security-oauth2-core', version: versions.springSecurity
    implementation group: 'org.springframework.security', name: 'spring-security-config', version: versions.springSecurity
    implementation group: 'org.springframework.security', name: 'spring-security-core', version: versions.springSecurity

    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.10'
    implementation group: 'org.liquibase', name: 'liquibase-core', version: '4.6.1'
    liquibaseRuntime group: 'org.liquibase', name: 'liquibase-core', version: '4.6.1'
    liquibaseRuntime group: 'org.liquibase.ext', name: 'liquibase-hibernate5', version: '4.6.1'
    liquibaseRuntime sourceSets.main.compileClasspath
    liquibaseRuntime sourceSets.main.output
    liquibaseRuntime group: 'org.liquibase', name: 'liquibase-groovy-dsl', version: '3.0.2'
    liquibaseRuntime group: 'org.postgresql', name: 'postgresql', version: versions.postgresql
    liquibaseRuntime group: 'com.h2database', name: 'h2'
    implementation group: 'org.postgresql', name: 'postgresql', version: versions.postgresql
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
    implementation group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'
    implementation group: 'commons-validator', name: 'commons-validator', version: '1.7'
    implementation group: 'javax.transaction', name: 'javax.transaction-api'
    implementation group: 'org.ehcache', name: 'ehcache', version: '3.9.9'

    implementation group: 'com.zaxxer', name: 'HikariCP', version: '5.0.0'
    implementation group: 'javax.el', name: 'javax.el-api', version: '3.0.0'
    implementation group:'org.hibernate', name: 'hibernate-core', version: versions.hibernate
    implementation group:'org.hibernate', name: 'hibernate-jcache', version: versions.hibernate
    implementation group:'org.hibernate', name: 'hibernate-entitymanager', version: versions.hibernate
    implementation group:'org.hibernate', name: 'hibernate-envers', version: versions.hibernate

    implementation group: 'com.github.hmcts', name: 'auth-checker-lib', version: '2.1.5'
    implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '4.0.2'
    implementation group: 'com.github.hmcts', name: 'idam-java-client', version: '2.0.1'
    implementation 'com.github.hmcts:ccd-case-document-am-client:1.7.1'
    implementation group: 'org.apache.tika', name: 'tika-core', version: '2.1.0'

    implementation group: 'org.mapstruct', name: 'mapstruct-jdk8', version: versions.mapstruct

    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'

    annotationProcessor group: 'org.mapstruct',name: 'mapstruct-processor', version: versions.mapstruct
    annotationProcessor group:'org.hibernate', name: 'hibernate-jpamodelgen', version: versions.hibernate
    annotationProcessor group:'org.springframework.boot', name: 'spring-boot-configuration-processor', withoutStuff

    testImplementation group:'org.springframework.boot', name: 'spring-boot-starter-test', withoutStuff
    testImplementation group: 'com.h2database', name: 'h2', version: '1.4.200'
    implementation group: 'com.github.gmazzo', name: 'okhttp-mock', version: '1.5.0'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: versions.logging
    implementation group: 'com.github.hmcts.java-logging', name: 'logging-appinsights', version: versions.logging
    implementation group: 'com.github.hmcts.java-logging', name: 'logging-spring', version: versions.logging
    
    annotationProcessor group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
    annotationProcessor group: 'com.sun.xml.bind', name: 'jaxb-core', version: '3.0.2'
    annotationProcessor group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '3.0.2'

    testRuntimeOnly group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '5.6.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.8.2'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.3'
    testImplementation group: 'org.pitest', name: 'pitest', version:'1.5.2'
    testImplementation group: 'info.solidsoft.gradle.pitest',  name: 'gradle-pitest-plugin', version: '1.5.2'

    testImplementation group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenityRestAssured, withoutStuff
    testImplementation group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity, withoutStuff
    testImplementation group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenity
    testImplementation group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity
    testImplementation group: 'net.serenity-bdd', name: 'serenity-single-page-report', version: versions.serenityreporter

    testImplementation group: 'com.github.hmcts', name: 'fortify-client', version: '1.2.2', classifier: 'all'

    contractTestImplementation group: 'au.com.dius', name: 'pact-jvm-consumer-junit', version: versions.pact_version, withoutNetty
    contractTestImplementation group: 'au.com.dius', name: 'pact-jvm-consumer-junit5', version: versions.pact_version, withoutNetty
    contractTestImplementation group: 'au.com.dius', name: 'pact-jvm-consumer-java8', version: versions.pact_version, withoutNetty
    implementation group: 'au.com.dius', name: 'pact-jvm-consumer', version: versions.pact_version, withoutNetty

    contractTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version:'5.6.3'
    contractTestRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.3'

    contractTestImplementation sourceSets.main.runtimeClasspath
    contractTestImplementation sourceSets.test.runtimeClasspath

    contractTestImplementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
    contractTestImplementation group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.13'
    contractTestImplementation group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.13'

    aatImplementation group: 'com.github.hmcts', name: 'em-test-helper', version: '1.20.3'
    aatImplementation group: 'org.apache.poi', name: 'poi', version: '5.0.0'
    aatImplementation group: 'org.apache.poi', name: 'poi-ooxml', version: '5.0.0'

}

dependencyManagement {
    dependencies {

        //CVE-2020-15824, CVE-2020-29582
        dependencySet(group: 'org.jetbrains.kotlin', version: '1.5.0-M1') {
            entry 'kotlin'
            entry 'kotlin-stdlib'
            entry 'kotlin-reflect'
            entry 'kotlin-stdlib-common'
            entry 'kotlin-stdlib-jdk7'
            entry 'kotlin-stdlib-jdk8'
        }
        
        
        //CVE-2021-29425
        dependencySet(group: 'commons-io', version: '2.11.0') {
            entry 'commons-io'
        }

        // Do not change - START
        // Changing below will break Serenity Report generation
        dependencySet(group: 'junit', version: '4.13') {
            entry 'junit'
        }

        dependencySet(group: 'org.junit.jupiter', version: '5.8.2') {
            entry 'junit-jupiter'
            entry 'junit-jupiter-api'
            entry 'junit-jupiter-params'
        }

        dependencySet(group: 'org.junit.vintage', version: '5.6.2') {
            entry 'junit-vintage-engine'
        }

        dependencySet(group: 'net.bytebuddy', version: '1.12.3') {
            entry 'byte-buddy'
            entry 'byte-buddy-agent'
        }
        // Do not change - END

        //CVE-2021-22118, CVE-2021-22096
        dependencySet(group: 'org.springframework', version: '5.3.13') {
            entry 'spring-aop'
            entry 'spring-aspects'
            entry 'spring-beans'
            entry 'spring-context'
            entry 'spring-core'
            entry 'spring-expression'
            entry 'spring-jcl'
            entry 'spring-jdbc'
            entry 'spring-orm'
            entry 'spring-tx'
            entry 'spring-web'
            entry 'spring-webmvc'
        }

        //CVE-2021-27568
        dependencySet(group: 'net.minidev', version: '2.4.7') {
            entry 'json-smart'
        }

        //CVE-2021-22044
        dependencySet(group: 'org.springframework.cloud', version: '3.0.5') {
            entry 'spring-cloud-openfeign-core'
            entry 'spring-cloud-starter-openfeign'
        }

        dependencySet(group: 'org.apache.logging.log4j', version: versions.log4JVersion) {
            entry 'log4j-core'
            entry 'log4j-api'
            entry 'log4j-to-slf4j'
        }
    }
}

bootJar {
    getArchiveFileName().set(provider {
        'rpa-native-pdf-annotator-app.jar'
    })
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

def coverageExclusionList = [
    '**uk/gov/hmcts/reform/em/npa/Application*',
    '**uk/gov/hmcts/reform/em/npa/appinsights/*',
    '**uk/gov/hmcts/reform/em/npa/config/*',
    '**uk/gov/hmcts/reform/em/npa/config/logging/*',
    '**uk/gov/hmcts/reform/em/npa/domain/*',
    '**uk/gov/hmcts/reform/em/npa/service/dto/external/annotation/*',
    '**uk/gov/hmcts/reform/em/npa/config/security/*',
    '**uk/gov/hmcts/reform/em/npa/ccd/domain/*',
    '**uk/gov/hmcts/reform/em/npa/ccd/dto/*',
    '**uk/gov/hmcts/reform/em/npa/ccd/exception/*',
    '**uk/gov/hmcts/reform/em/npa/service/exception/*',
    '**uk/gov/hmcts/reform/em/npa/service/dto/redaction/*',
    '**uk/gov/hmcts/reform/em/npa/info/*',
    '**/uk/gov/hmcts/reform/em/npa/config/audit/AsyncEntityAuditEventWriter*',
    '**uk/gov/hmcts/reform/em/npa/rest/errors/ExceptionTranslator*',
    '**uk/gov/hmcts/reform/em/npa/service/dto/AbstractAuditingDTO*',
    '**uk/gov/hmcts/reform/em/npa/rest/ResponseUtil*',
    '**uk/gov/hmcts/reform/em/npa/service/impl/DmStoreDownloaderImpl*',
    '**uk/gov/hmcts/reform/em/npa/service/impl/DmStoreUploaderImpl*'

]

checkstyle {
    maxWarnings = 10
    toolVersion = '8.12'
    ignoreFailures = true
    // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
    getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

checkstyleMain.shouldRunAfter(compileJava)
test.shouldRunAfter(checkstyleTest)

jacocoTestReport {
    executionData(test)

    reports {
        xml.required = true
        csv.required = false
        xml.outputLocation =  file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }

}

project.tasks['sonarqube'].dependsOn test, jacocoTestReport

sonarqube {
    properties {
        property "sonar.projectName", "${rootProject.name}"
        property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.destination.path}"
        property "sonar.exclusions", coverageExclusionList.join(", ")
        property "sonar.test.exclusions", "**/test/**"
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"

        property "sonar.issue.ignore.multicriteria", "e1"
        property "sonar.issue.ignore.multicriteria.e1.ruleKey", "java:S2629"
        property "sonar.issue.ignore.multicriteria.e1.resourceKey", "src/main/java/uk/gov/hmcts/reform/em/npa/**"
    }
}

idea {
    module {
        testSourceDirs += project.sourceSets.aat.java.srcDirs
        testResourceDirs += project.sourceSets.aat.resources.srcDirs
        testSourceDirs += project.sourceSets.contractTest.java.srcDirs
        testResourceDirs += project.sourceSets.contractTest.resources.srcDirs
    }
}


test {
    useJUnitPlatform()
}

task functional(type: Test) {
    group = 'Delivery pipeline'
    description = 'Executes functional tests'
    setTestClassesDirs(sourceSets.aat.output.classesDirs)
    setClasspath(sourceSets.aat.runtimeClasspath)
    include "uk/gov/hmcts/reform/em/npa/functional/**"

    // Serenity Single page Reports
    // functional.finalizedBy(aggregate)

    environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
}

task smoke(type: Test) {
    group = 'Delivery pipeline'
    description = 'Executes non-destructive smoke tests'
    setTestClassesDirs(sourceSets.aat.output.classesDirs)
    setClasspath(sourceSets.aat.runtimeClasspath)
    include "uk/gov/hmcts/reform/em/npa/smoke/**"

    environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
}

task migratePostgresDatabase(dependsOn: 'liquibaseUpdate')


task contract(type: Test) {
    group = 'Delivery pipeline'
    description = 'Runs the consumer Pact tests'
    useJUnitPlatform()
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
    include "uk/gov/hmcts/reform/em/npa/**"
    systemProperty 'pact.rootDir', "pacts"
    environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
}

task runAndPublishConsumerPactTests(type: Test){
    logger.lifecycle("Runs pact Tests")
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath

}

task fortifyScan(type: JavaExec)  {
    mainClass.set("uk.gov.hmcts.fortifyclient.FortifyClientMainApp")
    classpath += sourceSets.test.runtimeClasspath
    jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

runAndPublishConsumerPactTests.dependsOn contract

runAndPublishConsumerPactTests.finalizedBy pactPublish


project.ext {
    pactVersion = getCheckedOutGitCommitHash()
}

static def getCheckedOutGitCommitHash() {
    'git rev-parse --verify --short HEAD'.execute().text.trim()
}

serenity {
    reports = ["single-page-html"]
}

pact {
    publish {
        pactDirectory = 'pacts'
        pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'https://localhost:80'
        version = project.pactVersion
    }
}

compileJava.dependsOn processResources

pitest {

    logger.lifecycle("Runs PIT  Tests")
    targetClasses = ['uk.gov.hmcts.reform.em.npa.*']

    excludedClasses = [
            'uk.gov.hmcts.reform.em.npa.appinsights.*' ,
            'uk.gov.hmcts.reform.em.npa.domain.*' ,
            'uk.gov.hmcts.reform.em.npa.info.*',
            'uk.gov.hmcts.reform.em.npa.Application.java',
            'uk.gov.hmcts.reform.em.npa.infrastructure.*'

    ]
    enableDefaultIncrementalAnalysis = true
    historyInputLocation = 'build/reports/pitest/fastermutationtesting'
    historyOutputLocation = 'build/reports/pitest/fastermutationtestingoutput'
    threads = 15
    testSourceSets = [sourceSets.test]
    mainSourceSets = [sourceSets.main]
    fileExtensionsToFilter.addAll('xml','json')
    outputFormats = ['XML', 'HTML','CSV']
    coverageThreshold = 0
    maxMutationsPerClass = 15
    jvmArgs = ['-Xms1G','-Xmx3G']
    timestampedReports = false
    failWhenNoMutations = false
    detectInlinedCode = true
    threads = 10
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    failWhenNoMutations = false
}

project.tasks['pitest'].group = "Verification"
